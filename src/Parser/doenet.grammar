@top Document { topLevel+ }

entity {
  Text |
  Element |
  Comment |
  MismatchedCloseTag |
  Macro |
  incompleteStartCloseTag
}

Element {
  OpenTag entity* (CloseTag | MissingCloseTag) |
  SelfClosingTag
}

topLevel {
  Text |
  Comment |
  Macro |
  Element
}

OpenTag[closedBy="CloseTag MissingCloseTag"] {
  StartTag TagName Attribute* endTag
}

SelfClosingTag {
  StartTag TagName Attribute* selfCloseEndTag
}

CloseTag[openedBy=OpenTag] {
  StartCloseTag TagName  endTag
}

MismatchedCloseTag {
  mismatchedStartCloseTag TagName endTag
}

Attribute {
  AttributeName |
  AttributeName Is AttributeValue
}

AttributeValue {singleQuoteAttributeValue | doubleQuoteAttributeValue}

Comment { beginComment commentContent* "-->" }

@context elementContext from "./tokens.js"

@external tokens startTag from "./tokens.js" {
  StartTag[closedBy="SelfCloseEndTag EndTag"]
  StartCloseTag
  MissingCloseTag
  mismatchedStartCloseTag[@name=StartCloseTag]
  incompleteStartCloseTag[@name=StartCloseTag]
}

@external tokens commentContent from "./tokens.js" { commentContent }

singleQuoteAttributeValue { "'" (attributeContentSingleQuote | Macro)* "'" }
doubleQuoteAttributeValue { "\"" (attributeContentDoubleQuote | Macro)* "\"" }

@tokens {
  endTag { ">" }
  
  beginComment { "<!--" }

  selfCloseEndTag { "/>" }

  nameChar {
    std.asciiLetter | "-" | "_" | std.digit 
  }

  identifier { nameChar+ }

  TagName { identifier }

  Macro { "$" "$"? identifier | "$" "$"? "(" identifier ")" }

  AttributeName { identifier }

  attributeContentSingleQuote { "$" |  !['$ \t\r\n]  !['$]* }

  attributeContentDoubleQuote { "$" |  !["$ \t\r\n]  !["$]* }

  Is { "=" }

  Text { ![<$]+  }


  @precedence {Macro, attributeContentSingleQuote}
  @precedence {Macro, attributeContentDoubleQuote}
  @precedence {Text}

}
@detectDelim
